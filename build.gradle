plugins {
    id "dev.architectury.loom" version "0.12.0-SNAPSHOT"
    id 'maven-publish'
    id "me.shedaniel.unified-publishing" version "0.1.10"
    id "com.github.breadmoirai.github-release" version "2.4.1"
}

version = project.mod_version
group = project.maven_group

repositories {

}

dependencies {
    forge "net.minecraftforge:forge:${minecraft_version}-${forge_version}"

    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
}

loom {
    forge {
        mixinConfig "forgedfabric.mixins.json"
    }
}

processResources {
    inputs.property "version", project.version
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

def targetJavaVersion = 8
tasks.withType(JavaCompile).configureEach {
    // ensure that the encoding is set to UTF-8, no matter what the system default is
    // this fixes some edge cases with special characters not displaying correctly
    // see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
    // If Javadoc is generated, this must be specified in that task too.
    it.options.encoding = "UTF-8"
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        it.options.release = targetJavaVersion
    }
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
    archivesBaseName = project.archives_base_name
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}

// https://github.com/shedaniel/unified-publishing
// gradlew publishUnified
unifiedPublishing {
    project {
        displayName = project.archives_base_name + "-" + project.version
        version = project.version
        changelog = "See https://moddingtutorials.org/mods/ForgedFabric/Changelog for full details"
        releaseType = "beta"
        gameVersions = ["1.16.5"]
        gameLoaders = ["forge"]

        mainPublication tasks.remapJar

        curseforge {
            token = project.findProperty("CURSEFORGE_API_KEY") ?: System.getenv("CURSEFORGE_API_KEY")
            id = "662687"
        }
    }
}

// gradlew githubRelease
githubRelease {
    token project.findProperty("GH_API_KEY") ?: System.getenv("GH_API_KEY")
    owner "LukeGrahamLandry"
    repo "ForgedFabric"
    tagName "${project.version}"
    targetCommitish "1.16"
    releaseName "${project.version}"
    body "See https://moddingtutorials.org/mods/ForgedFabric/Changelog for full details"
    generateReleaseNotes false
    draft false
    releaseAssets remapJar.archiveFile
    allowUploadToExisting false
    overwrite false
    apiEndpoint "https://api.github.com"
    client
}

// from github: Actions > Publish > Run Workflow > Run Workflow
tasks.register("publishMod") {
    dependsOn(build)
    finalizedBy(tasks.publishUnified)
    finalizedBy(tasks.githubRelease)
}